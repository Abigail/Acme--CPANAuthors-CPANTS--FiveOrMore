#!/usr/bin/perl

use 5.010;

use strict;
use warnings;
no  warnings 'syntax';

use autodie;

use LWP::Simple;

my $dest = "lib/Acme/CPANAuthors/CPANTS/FiveOrMore.pm";

my $pat = '(?:[^<]+)';

my $URL = 'http://cpants.perl.org/highscores/many?page=';

my %AUTHORS;
my $page_count = 0;
while (1) {
    my $url = $URL . ++ $page_count;

    my $text = get $url or die;

    my $seen;
    while ($text =~ m{<td><a \s+ href="/author/[^"]+">(?<ID>$pat)</td>
                      <td>(?<AUTHOR>$pat)</td>}gx) {
        $seen = 1;
        my ($ID, $AUTHOR) = @+ {"ID", "AUTHOR"};
        $AUTHORS {$ID} = $AUTHOR;
    }
    last unless $seen;
}

my $LIST = "\n";
foreach my $id (sort keys %AUTHORS) {
    my $author = $AUTHORS {$id};
    $LIST .= "    q <$id> => q <$author>,\n";
}


open my $fh, ">", $dest;
print $fh do {local $" = ", "; <<"--"};
package Acme::CPANAuthors::CPANTS::FiveOrMore;

#
# THIS IS A GENERATED FILE. DO NOT UPDATE DIRECTLY.
#

use 5.006;
use strict;
use warnings;
no  warnings 'syntax';

our \$VERSION = '2009110901';

use Acme::CPANAuthors::Register ($LIST);

1;

__END__

=head1 NAME

Acme::CPANAuthors::CPANTS::FiveOrMore - CPAN Authors with five or more dists

=head1 SYNOPSIS

 use Acme::CPANAuthors;

 my \$authors  = Acme::CPANAuthors -> new ("CPANTS::FiveOrMore");

 my \$number   = \$authors -> count;
 my \@ids      = \$authors -> id;
 my \@distros  = \$authors -> distributions ("ABIGAIL");
 my \$url      = \$authors -> avatar_url    ("ABIGAIL");
 my \$kwalitee = \$authors -> kwalitee      ("ABIGAIL");
 my \$name     = \$authors -> name          ("ABIGAIL");

See documentation for Acme::CPANAuthors for more details.

=head1 DESCRIPTION

This class provides a hash of PAUSE IDs and names of CPAN authors
who have five or more distributions in the CPANTS database.

=head1 DEVELOPMENT

The current sources of this module are found on github,
L<< git://github.com/Abigail/Acme--CPANAuthors--CPANTS--FiveOrMore.git >>.

=head1 AUTHOR

Abigail, L<< mailto:cpan\@abigail.be >>.

=head1 COPYRIGHT and LICENSE

Copyright (C) 2009 by Abigail.

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the "Software"),   
to deal in the Software without restriction, including without limitation
the rights to use, copy, modify, merge, publish, distribute, sublicense,
and/or sell copies of the Software, and to permit persons to whom the
Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
THE AUTHOR BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT
OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.

=head1 INSTALLATION

To install this module, run, after unpacking the tar-ball, the 
following commands:

   perl Makefile.PL
   make
   make test
   make install

=cut
--

close $fh;

__END__
